# Space as we think of it in the physical universe, with coordinates.
#
# Standard shapes that can exist in space.

Coordinates is a Location
Coordinates has a Number named x: 0, a Number named y: 0, a Number named z: 0.
SpacePoint is a DimensionPoint where:
    Location is Coordinates
SpaceForm is a Form where:
    All DimensionPoint are SpacePoint

Circle is a SpaceForm.
Circle has a Number: radius, 1 Coordinates: center

# TODO: Write tests for Rectangle's invariant, or redesign the language to auto-generate the tests.

Rectangle is a SpaceForm.
Rectangle has 4 Coordinates: topleft, topRight, bottomRight, bottomLeft
Rectangle can be created by Create2D using a Coordinates named origin, a Number named width, a Number named height:
    topLeft is origin
    topRight is a duplicate of origin
    topRight's X += width
    bottomRight is a duplicate of topRight
    bottomRight's Y += height
    bottomLeft is a duplicate of topLeft
    bottomLeft's Y += height

# TODO: we should auto-generate tests for "guarantees" when possible
Rectangle's Create2D guarantees isRectangle

# TODO: How can the compiler enforce these invariants?
#
# These are the mathematical checks required to prove that a rectangle
# is a valid rectangle even in 3D space. Even though an angled rectangle
# in 3D space will be unusual, the math to prove it isn't especially more
# expensive than the math to prove a 2D rectangle, so we allow 3D Rectangles.
Rectangle invariant named isRectangle:
    # Rectangles must be a parallelogram.
    #
    # Geometrically, this means the diagonals of any rectangle (or
    # parallelogram) must bisect each other, meaning they share the
    # exact same midpoint.
    #
    # Instead of calculating averages (which requires slower division),
    # we simply check if the sums of the opposite corners are equal.
    #
    # These checks perform three critical jobs:
    #
    # 1. They guarantee the four points lie on the same flat 3D plane (coplanar).
    # 2. They guarantee opposite sides are parallel.
    # 3. They catch "Winding Order" errors (e.g., if the user specified points in a Z-pattern).
    topLeft's x + bottomRight's x == topRight's x + bottomLeft's x
    topLeft's y + bottomRight's y == topRight's y + bottomLeft's y
    topLeft's z + bottomRight's z == topRight's z + bottomLeft's z

    # Size Check
    # A rectangle must have width and height. If adjacent corners are the exact
    # same point, the shape is collapsed into a line segment or a single dot.
    # We explicitly check for this "degenerate" state.
    not (topLeft's x == topRight's x
         and topLeft's y == topRight's y
         and topLeft's z == topRight's z)
    not (topRight's x == bottomRight's x
         and topRight's y == bottomRight's y
         and topRight's z == bottomRight's z)

    # The 90-Degree Angle Check
    #
    # The steps above prove we have a valid Parallelogram. Now we must prove
    # it is a Rectangle by checking for right angles.
    #
    # We do this using the "Dot Product" of two adjacent edge vectors.
    # Formula: (Vector A â€¢ Vector B) = Sum(Ax*Bx, Ay*By, Az*Bz)
    #
    # If the Dot Product is exactly 0, the two edges are perpendicular.
    xDotProduct = (topRight's x - topLeft's x) * (bottomRight's x - topRight's x)
    yDotProduct = (topRight's y - topLeft's y) * (bottomRight's y - topRight's y)
    zDotProduct = (topRight's z - topLeft's z) * (bottomRight's z - topRight's z)
    xDotProduct + yDotPRoduct + zDotProduct == 0